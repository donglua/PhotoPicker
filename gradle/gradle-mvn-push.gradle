
apply plugin: 'com.jfrog.bintray'

version = "0.8.9"
def siteUrl = 'https://github.com/donglua/PhotoPicker'      // Homepage URL of the library
def gitUrl  = 'https://github.com/donglua/PhotoPicker.git'  // Git repository URL
group = "me.iwf.photopicker"                                // Maven Group ID for the artifact

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
tasks.withType(JavaCompile) { options.encoding = "UTF-8" }

artifacts {
    //  archives javadocJar
    archives sourcesJar
}

def localProperties = project.rootProject.file('local.properties')

Properties properties = new Properties()

if (localProperties.exists()) {
    def stream = localProperties.newDataInputStream()
    properties.load(stream)
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publish = true
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "PhotoPicker"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
